// Top-level build file where you can add configuration options common to all sub-projects/modules.

def versionMajor = 0
def versionMinor = 9
def versionPatch = 7
def versionCodeForLocalDevelopment = 26
def computedVersionCode = (System.getenv("VERSION_CODE") ?: versionCodeForLocalDevelopment) as Integer

buildscript {
    repositories {
        google()
        jcenter()
    }

    ext {
        gradleVersion = '3.1.2'
        kotlinVersion = '1.2.40'
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.25.0"
    }
}

allprojects {
    repositories {
        maven {
            url "https://maven.google.com"
        }
        maven {
            url "https://jitpack.io"
        }
        maven {
            url "https://maven.fabric.io/public"
        }
        google()
        jcenter()
    }
}

// This block encapsulates custom properties and makes them available to all
// modules in the project.
ext {
    minSdkVersion = 24
    targetSdkVersion = 28
    compileSdkVersion = 28
    applicationId = "org.watsi.claims_submission"
    versionCode = computedVersionCode
    versionName = "${versionMajor}.${versionMinor}.${versionPatch} (${computedVersionCode})"
}

// This adds more custom properties from the dependencies.gradle file and makes them available to
// all modules in the project.
apply from: 'dependencies.gradle'
apply plugin: "com.github.ben-manes.versions"

task clean(type: Delete) {
    delete rootProject.buildDir
}

task runDomainUnitTests(dependsOn: [':domain:test']) {
    description 'Run unit tests for the domain module.'
}

task runDeviceUnitTests(dependsOn: [':device:testDebugUnitTest']) {
    description 'Run unit tests for the device module.'
}

task runAppUnitTests(dependsOn: [':app:testEthiopiaDebugUnitTest', ':app:testUgandaDebugUnitTest']) {
    description 'Run unit tests for the app module.'
}

task runUnitTests(dependsOn: ['runDomainUnitTests', 'runDeviceUnitTests', 'runAppUnitTests']) {
    description 'Run unit tests for all modules.'
}

task runAppInstrumentationTests(dependsOn: [':app:connectedAndroidTest']) {
    description 'Run instrumentation tests for the app module.'
}
